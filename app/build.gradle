
plugins {
    id 'io.gh.boundaries.coffeestore.java-application-conventions'
    alias(libs.plugins.openapi.gen)
}

dependencies {
    implementation project(':utilities')
}

openApiGenerate {
    generatorName = 'spring'
    inputSpec = "$rootDir/app/src/main/resources/openapi.yml".toString()
    outputDir = "$buildDir/generated".toString()

    apiPackage = 'io.gh.boundaries.coffeestore.web'
    modelPackage = 'io.gh.boundaries.coffeestore.web'

    configOptions = [
        configPackage: 'io.gh.boundaries.coffeestore.configuration',
        dateLibrary: 'java8',
        requestMappingMode: 'api_interface',
        interfaceOnly: "true",
        skipDefaultInterface: "true",
        documentationProvider: "none",
        openApiNullable: "false",
        useSwaggerUI: "false"
    ]
}
compileJava.dependsOn tasks.openApiGenerate

sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java".toString()
        }
    }
}

tasks.named("bootRun") {
    systemProperty 'spring.datasource.url', 'jdbc:postgresql://localhost:15432/coffeebags'
    systemProperty 'spring.datasource.username', 'user'
    systemProperty 'spring.datasource.password', 'password'
}